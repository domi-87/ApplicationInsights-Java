/*
 * ApplicationInsights-Java
 * Copyright (c) Microsoft Corporation
 * All rights reserved.
 *
 * MIT License
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this
 * software and associated documentation files (the ""Software""), to deal in the Software
 * without restriction, including without limitation the rights to use, copy, modify, merge,
 * publish, distribute, sublicense, and/or sell copies of the Software, and to permit
 * persons to whom the Software is furnished to do so, subject to the following conditions:
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 * THE SOFTWARE IS PROVIDED *AS IS*, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
 * PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
 * FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
 * OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */

apply from: "$buildScriptsDir/common-java.gradle"
apply from: "$buildScriptsDir/publishing.gradle"

def sdkVersionFileDir = "$project.buildDir/src/generated/main/resources"

sourceSets {
    main {
        resources {
            srcDir sdkVersionFileDir
        }
    }
}

import com.microsoft.applicationinsights.build.tasks.PropsFileGen

archivesBaseName = 'applicationinsights-core'

configurations {
    forTheDLL { transitive = false }
}

dependencies {
    implementation ([group: 'eu.infomas', name: 'annotation-detector', version: '3.0.5'])
    implementation ([group: 'commons-io', name: 'commons-io', version: '2.6' ])
    implementation ([group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'])
    implementation ([group: 'org.apache.commons', name: 'commons-text', version: '1.9'])
    implementation ([group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'])
    implementation ([group: 'com.squareup.moshi', name: 'moshi', version: '1.8.0'])
    implementation ([group: 'com.google.code.gson', name: 'gson', version: '2.8.2'])
    // update transitive dependency version
    implementation ([group: 'com.google.guava', name: 'guava', version: '27.1-android'])
    implementation ([group: 'com.github.oshi', name:'oshi-core',version:'5.6.0'])
    implementation 'org.slf4j:slf4j-api:1.7.26'
    testImplementation group: 'org.hamcrest', name:'hamcrest-core', version:'1.3'
    testImplementation group: 'org.hamcrest', name:'hamcrest-library', version:'1.3'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'
    testImplementation group: 'io.github.hakky54', name: 'logcaptor', version: '2.3.1'

    forTheDLL 'com.microsoft.azure:applicationinsights-core:2.6.2'
}

task copyTheDLL(type: Copy) {
    from zipTree(configurations.forTheDLL.singleFile).matching {
        include '*.dll'
    }
    into "$buildDir/resources/main"
}

processResources.dependsOn copyTheDLL

task generateVersionProperties(type: PropsFileGen) {
    targetFile = new File(sdkVersionFileDir, "sdk-version.properties")
    property "version", project.version
}

processResources.dependsOn generateVersionProperties

// region Publishing properties

projectPomName = project.msftAppInsights + " Java SDK Core"
projectPomDescription = "This is the core module of " + project.msftAppInsightsJavaSdk

whenPomConfigured = { p ->
    p.dependencies = []
}

// endregion Publishing properties
