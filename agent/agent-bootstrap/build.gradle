import com.microsoft.applicationinsights.build.tasks.PropsFileGen

apply from: "$buildScriptsDir/common-java.gradle"

repositories {
    mavenLocal()
}

def sdkVersionFileDir = "$project.buildDir/src/generated/main/resources"

sourceSets {
    main {
        resources {
            srcDir sdkVersionFileDir
        }
    }
}

task generateVersionProperties(type: PropsFileGen) {
    targetFile = new File(sdkVersionFileDir, "ai.sdk-version.properties")
    property "version", project.version
}

processResources.dependsOn generateVersionProperties

dependencies {
    implementation (group: 'io.opentelemetry.javaagent', name: 'opentelemetry-javaagent-bootstrap', version: '1.0.0+ai.patch.1-alpha') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
    implementation group: 'io.opentelemetry.instrumentation', name: 'opentelemetry-instrumentation-api', version: '1.0.0+ai.patch.1-alpha'
    implementation group: 'io.opentelemetry.javaagent', name: 'opentelemetry-javaagent-api', version: '1.0.0+ai.patch.1-alpha'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'

    // not using gson because it has dependency on java.sql.*, which is not available in Java 9+ bootstrap class loader
    // only complaint so far about moshi is that it doesn't give line numbers when there are json formatting errors
    implementation 'com.squareup.moshi:moshi:1.9.3'

    implementation(project(path: ':etw:java'))

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'com.google.guava:guava:27.1-android'
}
